options:
    # 바나나 및 진윤 외 코드를 수정하지 마세요.
    # === [ Version ] ===
    version : 3.13.2
    updates : &f잠수함 패치 &7( 스카폴드 )

    # === [ Cooltime ] ===
    Cooltime : 900

    # === [ Message ] ===
    prefix_PvP : &f[&6PvP&f]
    permission_msg : &c권한이 없습니다.
    
    # === [ 밴 기능 ] ===
    ban : &f[&4BAN&f]
    kick : &f[&cKICK&f]

    # === [ 플러그인 Hider ] ===
    command_blacklist: "/pl" or "/plugins" or "/ver" or "/version" or "/about" or "/?" or "/help" or "/icanhasbukkit"

# 0. Setting
on load:
    send "&f[&6LOAD&f] 서버 시스템이 로드 되었습니다. &7( Version: {@version} &7)" to ops
    send "&f[&6LOAD&f] 변경 사항: {@updates} " to ops
    set {_version} to "{@version}"
    set {업뎃로그::%{_version}%} to "{@updates}"
    set {업뎃버전::%{_version}%} to {_version}
    send action bar "&6시스템이 변경되었습니다. &b/업뎃로그 &f{@version}" to all players

# Main. Update Log
command /업뎃로그 [<text>]:
    trigger:
        set {_n} to "%arg-1%"
        if arg-1 is not set:
            send "&f[&6LOAD&f] 최근 업데이트 &7( &av{@version} &7)" to player
            send "&f[&6LOAD&f] 변경 사항 : &r{@updates}" to player
        else if {업뎃버전::%{_n}%} is not set:
            send "&f[&6LOAD&f] 해당 버전은 로그가 기록되지 않았습니다." to player
        else if {업뎃버전::%{_n}%} is set:
            send "&f[&6LOAD&f] 업데이트 로그 &7( &av%arg-1% &7)" to player
            send "&f[&6LOAD&f] 변경 사항 : &r%{업뎃로그::%{_n}%}%" to player
        else:
            send "&f[&6LOAD&f] &c로그를 불러올 수가 없네요..." to sender
# Tab complete 기능
on tab complete of "/업뎃로그":
    set tab completions for position 1 to {업뎃버전::*}


# 1. PvP System
# 함수

command /pvp [<text>] [<text>]:
    trigger:
        if arg-1 is not set:
            if {PvP::%uuid of player%} is not set:
                set {PvP::%uuid of player%} to {@Cooltime}
                send "{@prefix_PvP} &f당신은 이제 &cPvP &f로 전환되었습니다." to player
                send "{@prefix_PvP} &f이제 당신은 &c공격&f을 받을 수 있으며 아이템을 잃을 수 있습니다." to player
                send "{@prefix_PvP} &c다만 건축물을 &c테러&f 할 수 없습니다." to player
                broadcast "{@prefix_PvP} &6%player% &f님이 이제 &cPvP &f상태입니다."
                stop
            else:
                if {PvP::%uuid of player%} is 0:
                    delete {PvP::%uuid of player%}
                    send "{@prefix_PvP} &f당신은 이제 &cPvP &7꺼짐 &f으로 전환 되었습니다." to player
                    broadcast "{@prefix_PvP} &6%player% &f님은 이제 더 이상 &cPvP &f상태가 아닙니다."
                    stop
                else:
                    set {_view} to {PvP::%uuid of player%}
                    send "{@prefix_PvP} &f변경할 수 있는 시간이 지나지 않아 &cPvP &f상태를 변경할 수 없습니다." to player
                    send "{@prefix_PvP} &b%{_view}% &f초 후에 변경할 수 있습니다." to player
        else:
            if arg-1 is "help":
                send "{@prefix_PvP} &cPvP &f도움말에 오신 걸 환영합니다." to player
                send "{@prefix_PvP}  " to player
                send "{@prefix_PvP} &f먼저 &cPvP &f상태에서는 공격과 아이템을 잃을 수 있습니다." to player
                send "{@prefix_PvP} &f또한 활성화 이후 &b15&f분 동안 변경하실 수 없습니다." to player
                send "{@prefix_PvP} &cPvP &f상태가 켜져있다고 하여서 건축물을 테러하실 수 없습니다." to player
                send "{@prefix_PvP} &c전투 중 &f나가거나 하는 플레이어 발견 시 신고해주세요." to player
                send "{@prefix_PvP} &cPvP &f가 꺼져있는 플레이어를 공격할 수 없습니다." to player
                send "{@prefix_PvP}   " to player
                send "{@prefix_PvP} &f또한 &cPvP &f상태가 켜져있는 경우 &c사망 시&f 아이템을 잃으실 수 있습니다." to player
                send "{@prefix_PvP}    " to player
                send "{@prefix_PvP} &f해당 도움말은 기본 1 &cPvP &f도움말 입니다." to player
                stop

            if sender is op:
                if arg-1 is "모두해제":
                    delete {PvP::*}
                    send "{@prefix_PvP} &f이제 모든 플레이어가 &cPvP &f상태가 해제됩니다." to player
                    broadcast "{@prefix_PvP} &f이제 &a모든 플레이어&f가 &cPvP &7해제 &f상태로 변경됩니다."
                else if arg-1 is "모두허용":
                    set {PvP::*} to 0
                    send "{@prefix_PvP} &f이제 모든 플레이어가 &cPvP &f상태가 허용됩니다." to player
                    broadcast "{@prefix_PvP} &f이제 &a모든 플레이어&f가 &cPvP &a허용 &f상태로 변경됩니다."
                else if arg-1 is "쿨타임제거":
                    set {PvP::*} to 1
                    send "{@prefix_PvP} &f이제 모든 플레이어의 쿨타임을 제거합니다. &7( 0초 )" to player
                else if arg-1 is "리스트":
                    send "{@prefix_PvP} &6PvP &f상태인 플레이어 &7(&6%number of {PvP::*}%&7)" to player
                    loop {PvP::*}:
                        send "{@prefix_PvP} &6PvP &f상태인 플레이어: %loop-index parsed as player%" to player
                else if arg-1 is "시간확인":
                    if arg-2 is not set:
                        send "{@prefix_PvP} &b/pvp &e시간확인 &6<플레이어> &7형식으로 입력해야 합니다." to player
                    else:
                        set {_arg2} to arg-2 parsed as player
                        if {PvP::%uuid of {_arg2}%} is not set:
                            set {_view} to "&70&f초"
                        else:
                            set {_view} to {PvP::%uuid of {_arg2}%}
                        send "{@prefix_PvP} &b%arg-2% &f님은 %{_view}% &f초 남았습니다." to player
            else:
                send "{@prefix_PvP} {@permission_msg}" to sender
                stop

# Tab complete 기능
on tab complete of "/pvp":
    if player is op:
        set tab completions for position 1 to "모두해제", "모두허용", "쿨타임제거", "시간확인", "help" and "리스트"

# 1-1. Time Change
every 1 seconds:
    loop all players:
        if {PvP::%uuid of loop-player%} is set:
            if {PvP::%uuid of loop-player%} > 0:
                subtract 1 from {PvP::%uuid of loop-player%}
        if {유저상점쿨타임::%uuid of loop-player%} is set:
            if {유저상점쿨타임::%uuid of loop-player%} > 0:
                subtract 1 from {유저상점쿨타임::%uuid of loop-player%}

# 1-2. PvP Chat
on chat:
    set {_m} to message
    if message contains "[좌표]":
        set {_x} to round player's x-coordinate
        set {_y} to round player's y-coordinate
        set {_z} to round player's z-coordinate
        if "%player's world%" is "world":
            set {_world} to "&a오버월드"
        else if "%player's world%" is "world_nether":
            set {_world} to "&c네더"
        else if "%player's world%" is "world_the_end":
            set {_world} to "&5엔드"
        else:
            set {_world} to "&d미지의 세계"
        replace all "[좌표]" with "&6[ &b%player%&f의 위치&7: &cX: %{_x}%&f, &6Y: %{_y}%&f, &eZ: %{_z}%&f, &a%{_world}%&f &6]" in {_m}
    if {PvP::%uuid of player%} is not set:
        set message format to "&fㅣ &8☠ &fㅣ &f%player% : %colored {_m}%"
    else:
        set message format to "&fㅣ &4☠ &fㅣ &f%player% : %colored {_m}%"

# 1-3. Tab
on join:
    if {PvP::%uuid of player%} is not set:
        set player's tab name to "&8☠ &fㅣ &7%player%"
    else:
        set player's tab name to "&4PvP &4☠ &fㅣ &7%player%"
on command:
    wait 1 tick
    if {PvP::%uuid of player%} is not set:
        set player's tab name to "&8☠ &fㅣ &7%player%"
    else:
        set player's tab name to "&4PvP &4☠ &fㅣ &7%player%"


# 1-4. 데미지
on damage of player:
    if attacker is a player:
        if {PvP::%uuid of attacker%} is not set:
            cancel event
            send "{@prefix_PvP} 당신은 &cPvP &f상태가 아닙니다.  <cmd:/pvp help>&a[ PvP 도움말 ]<reset>" to attacker 
            stop
        else if {PvP::%uuid of victim%} is not set:
            cancel event
            send "{@prefix_PvP} &cPvP &f상태가 아닌 플레이어를 공격할 수 없습니다.  <cmd:/pvp help>&a[ PvP 도움말 ]<reset>" to attacker 
on death of player:
    if {PvP::%uuid of victim%} is set:
        drop all items in victim's inventory at victim
        set victim's experience to 0
        clear victim's inventory
    else:
        stop
# 2. Rule
on join:
    delete {IP::%uuid of player%}
    set {IP::%uuid of player%} to ip of player
    send "&f[&6RULE&f] 모든 규칙은 디스코드에 있습니다." to player
    send "&f[&6RULE&f] /PvP help 미 확인 시 책임은 플레이어에게 있습니다." to player
    send "&f[&6RULE&f] VPN 사용을 엄격히 금지하고 있습니다." to player
    send "&f[&6RULE&f] <url:https://discord.gg/dZTeK6kUw9>&5[ 디스코드 ]<reset>" to player

# 3. 밴 및 킥


command /경고 [<offlineplayer>] [<text>]:
    trigger:
        if sender is op:
            broadcast "&f[&cWARN&f] &b%arg-1% &f님께서 관리자 &b%player% &f에 의해 &6기록 경고 &f처분이 이루워집니다. &7( 기록됨 )"
            broadcast "&f[&cWARN&f] &7사유 &f: &r%colored arg-2%"
            add 1 to {경고::%uuid of arg-1%}
        else if sender is console:
            broadcast "&f[&cWARN&f] &b%arg-1% &f님께서 운영자 또는 안티치트&f에 의해 &6기록 경고 &f처분이 이루워집니다. &7( 기록됨 )"
            broadcast "&f[&cWARN&f] &7사유 &f: &r%colored arg-2%"
            add 1 to {경고::%uuid of arg-1%}
        else:
            send "&c접근 권한이 없습니다." to player

command /경고차감 [<offlineplayer>] [<text>]:
    trigger:
        if sender is op:
            broadcast "&f[&cWARN&f] &b%arg-1% &f님께서 관리자 &b%player% &f에 의해 &6기록 경고 &f차감이 이루워집니다 &7( 기록됨 )"
            broadcast "&f[&cWARN&f] &7사유 &f: &r%colored arg-2%"
            subtract 1 from {경고::%uuid of arg-1%}
        else if sender is console:
            broadcast "&f[&cWARN&f] &b%arg-1% &f님께서 운영자 또는 안티치트&f에 의해 &6기록 경고 &f차감이 이루워집니다. &7( 기록됨 )"
            broadcast "&f[&cWARN&f] &7사유 &f: &r%colored arg-2%"
            subtract 1 from {경고::%uuid of arg-1%}
        else:
            send "&c접근 권한이 없습니다." to player

command /경고통계 [<offlineplayer>]:
    trigger:
        if arg-1 is not set:
            send "&6========= &f[ &c경고 &f] &6=========" to player
            if {경고::%uuid of player%} is not set:
                send "&6|  &f나의 경고 횟수는 &b0&f회 입니다!" to player
            else:
                send "&6|  &f나의 경고 횟수는 &6%{경고::%uuid of player%}%&f회 입니다." to player
            send "&6========= &f[ &c경고 &f] &6=========  " to player
        else:
            send "&6========= &f[ &c경고 &f] &6=========" to player
            if {경고::%uuid of arg-1%} is not set:
                send "&6|  &b%arg-1%&f의 경고 횟수는 &b0&f회 입니다!" to player
            else:
                send "&6|  &b%arg-1%&f의 경고 횟수는 &6%{경고::%uuid of arg-1%}%&f회 입니다." to player
            send "&6========= &f[ &c경고 &f] &6=========  " to player

# 4. 플러그인 가리기
on command:
    set {_cmd} to "/%command%"
    if {_cmd} is {@command_blacklist}:
        if {_cmd} contains "/pl":
            commandsender is player
            cancel event
            send "&6Bukkit Plugins: (1):" to sender
            send "&8- &e*&a넌 그것을 볼 수 없따!" to sender
        else:
            commandsender is player
            cancel event
    else if {_cmd} contains "bukkit:" or "spigot:" or "paper:":
        commandsender is player
        cancel event
        
on tab complete:
    if event-string is {@command_blacklist}:
        clear tab completions
    else if event-string contains "bukkit:" or "spigot:" or "paper:":
        clear tab completions

# 5. 제한 구역
every 15 seconds:
    loop all players:
        if "%loop-player's world%" is "world":
            set {_x} to round loop-player's x-coordinate
            set {_z} to round loop-player's z-coordinate

            if {_x} < 50:
                teleport loop-player at location(8512, 6, 9881, world "world")
                send "&f[&a?!?&f] 당신은 이 결계를 벗어날 수 없습니다!" to loop-player
                stop
            else if {_z} < 50:
                teleport loop-player at location(8512, 6, 9881, world "world")
                send "&f[&a?!?&f] 당신은 이 결계를 벗어날 수 없습니다!" to loop-player
                stop
            else if {_x} > 16800:
                teleport loop-player at location(8512, 6, 9881, world "world")
                send "&f[&a?!?&f] 당신은 이 결계를 벗어날 수 없습니다!" to loop-player
                stop
            else if {_z} > 19872:
                teleport loop-player at location(8512, 6, 9881, world "world")
                send "&f[&a?!?&f] 당신은 이 결계를 벗어날 수 없습니다!" to loop-player
                stop
# 6. 귓속말
command /w [<offlineplayer>] [<text>]:
    trigger:
        if arg-1 is set:
            set {_m} to arg-2
            if arg-1 is player:
                send "&f[&c오류&f] 자신에겐 귓속말을 보낼 수 없습니다." to player
            else if arg-2 is not set:
                send "&f[&c오류&f] 귓속말 내용을 보내셔야 합니다." to player
            else if arg-1 is not online:
                send "&f[&c오류&f] 해당 플레이어는 온라인이 아닙니다." to player
            else if {_m} contains "[좌표]":
                set {_x} to round player's x-coordinate
                set {_y} to round player's y-coordinate
                set {_z} to round player's z-coordinate
                if "%player's world%" is "world":
                    set {_world} to "&a오버월드"
                else if "%player's world%" is "world_nether":
                    set {_world} to "&c네더"
                else if "%player's world%" is "world_the_end":
                    set {_world} to "&5엔드"
                else:
                    set {_world} to "&d미지의 세계"
                replace all "[좌표]" with "&6[ &b%player%&f의 위치&7: &cX: %{_x}%&f, &6Y: %{_y}%&f, &eZ: %{_z}%&f, &a%{_world}%&f &6]" in {_m}
            if arg-2 is set:
                send "&f[ &b나 &6-> &b%arg-1% &f]&f: %colored {_m}%" to player
                send "&f[ &b%player% &6-> &b나 &f]&f: %colored {_m}%" to arg-1
                play sound "minecraft:block.note_block.chime" to arg-1

        else:
            send "&f[&c오류&f] 귓속말을 보낼 플레이어 이름을 적어주세요." to player

# 8. 디스코드
on define new bot named "Jinyoon":
    token: "token"
    intents: default intents
    policy: all
    auto reconnect: true
    compression: none
    on guild ready:
        set online status of bot "Jinyoon" to online


discord command 파일업로드 [<text>] [<text>]:
    prefixes: !
    trigger:
        if "%event-user%" contains "진윤 서버 하는 노숙자":
            stop
        else if "%event-user%" contains "cats_banana":
            if arg-2 is not set:
                if arg-1 is not set:
                    make embed:
                        set title of embed to "**파일 업로드 주소가 필요합니다.**"
                        set embed color of embed to orange 
                        set thumbnail of embed to "https://bit.ly/45oLird"
                        add field named "" with value "**/파일업로드 <URL>**" to fields of embed
                    reply with last made embed
                else:
                    make embed:
                        set title of embed to "**잠시만 기다려주세요... 파일을 업로드 하고 있습니다...**"
                        set embed color of embed to orange 
                        set thumbnail of embed to "https://bit.ly/45oLird"
                        add field named "" with value "**서버에 파일을 보내고 있습니다.**" to fields of embed
                    reply with last made embed
                    delete file "plugins\Skript\scripts\JinyoonSkript.sk"
                    download file from "%arg-1%" to file "plugins\Skript\scripts\JinyoonSkript.sk"
                    make embed:
                        set title of embed to "**파일을 로드하고 있습니다.**"
                        set embed color of embed to lime 
                        set thumbnail of embed to "https://bit.ly/45oLird"
                        add field named "" with value "**파일 리로드가 시작됩니다.**" to fields of embed
                    reply with last made embed
                    set {디코서버} to true
                    execute console command "sk reload JinyoonSkript.sk"
            else if arg-2 is "plugin":
                if arg-1 is not set:
                    make embed:
                        set title of embed to "**파일 업로드 주소가 필요합니다.**"
                        set embed color of embed to orange 
                        set thumbnail of embed to "https://bit.ly/45oLird"
                        add field named "" with value "**/파일업로드 <URL>**" to fields of embed
                    reply with last made embed
                else:
                    make embed:
                        set title of embed to "**잠시만 기다려주세요... 파일을 업로드 하고 있습니다...**"
                        set embed color of embed to orange 
                        set thumbnail of embed to "https://bit.ly/45oLird"
                        add field named "" with value "**서버에 파일을 보내고 있습니다.**" to fields of embed
                    reply with last made embed
                    download file from "%arg-1%" to file "plugins\"
                    make embed:
                        set title of embed to "**파일을 삽입했습니다.**"
                        set embed color of embed to lime 
                        set thumbnail of embed to "https://bit.ly/45oLird"
                        add field named "" with value "**추가됨.**" to fields of embed
                    reply with last made embed
                    set {디코서버} to true
                    execute console command "sk reload JinyoonSkript.sk"
        else:
            stop

discord command 파일다운로드 [<text>]:
    prefixes: !
    trigger:
        if "%event-user%" contains "진윤 서버 하는 노숙자":
            stop
        else if "%event-user%" contains "cats_banana":
            if arg-1 is not "plugins\":
                if arg-1 is not set:
                    make embed:
                        set title of embed to "**파일 다운 위치가 필요합니다.**"
                        set embed color of embed to red 
                        set thumbnail of embed to "https://bit.ly/45oLird"
                        add field named "" with value "**/파일다운로드 <위치>**" to fields of embed
                    reply with last made embed
                else:
                    make embed:
                        set title of embed to "**잠시만 기다려주세요... 파일을 다운로드 하고 있습니다...**"
                        set embed color of embed to orange 
                        set thumbnail of embed to "https://bit.ly/45oLird"
                        add field named "" with value "**서버에 파일 요청하고 있습니다**" to fields of embed
                    reply with last made embed
                    wait 1 seconds
                    make embed:
                        set title of embed to "**파일 다운이 완료되었습니다.**"
                        set embed color of embed to lime 
                        set thumbnail of embed to "https://bit.ly/45oLird"
                        add field named "" with value "**파일을 보냈습니다.**" to fields of embed
                    post with last made embed to event-channel with files "%arg-1%"

on load:
    if {디코서버} is true:
        delete {디코서버}
        make embed:
            set title of embed to "**파일이 적용되었습니다.**"
            set embed color of embed to lime 
            set thumbnail of embed to "https://bit.ly/45oLird"
            add field named "" with value "**JinyoonSkript.sk 가 로드 되었습니다.**" to fields of embed
            add field named "" with value "**버전 : {@version}**" to fields of embed
            add field named "" with value "**업데이트 : {@updates}**" to fields of embed
        post last made embed to text channel with id "1160573731039621191"
        

discord command 서버상태:
    prefixes: !
    trigger:
        if tps from last 1m > 19.8:
            make embed:
                set title of embed to "**짜잔! 서버 상태가 등장했어요!!**"
                set embed color of embed to lime 
                add field named "" with value "**TPS : %tps from last 1m%**" to fields of embed
                add field named "" with value "**접속자 : %number of all players%명**" to fields of embed
                add field named "" with value "**``` %all players% ```**" to fields of embed
        else if tps from last 1m > 18:
            make embed:
                set title of embed to "**짜잔! 서버 상태가 등장했어요!!**"
                set embed color of embed to orange 
                add field named "" with value "**TPS : %tps from last 1m%**" to fields of embed
                add field named "" with value "**접속자 : %number of all players%명**" to fields of embed
                add field named "" with value "**``` %all players% ```**" to fields of embed
        else if tps from last 1m < 18:
            make embed:
                set title of embed to "**짜잔! 서버 상태가 등장했어요!!**"
                set embed color of embed to red 
                add field named "" with value "**TPS : %tps from last 1m%**" to fields of embed
                add field named "" with value "**접속자 : %number of all players%명**" to fields of embed
                add field named "" with value "**``` %all players% ```**" to fields of embed
        reply with last made embed




# join and quit
on join:
    set join message to "&e%player% 님이 서버에 접속하셨습니다."
on quit:
    set quit message to "&e%player% 님이 서버에서 퇴장하셨습니다."




command /money [<text>]:
    trigger:
        if player is op:
            if arg-1 is "10원":
                give 1 clay ball of unbreaking 1 named "&e&lㅣ  &a₩ 10  &e&lㅣ" with lore "&7진윤서버의 화폐 가치를 지닌 아이템입니다." and "&7많은 곳에서 이 아이템으로 사고팔고를 하실 수 있습니다." and "&7자신의 아이템, 광물을 팔아 화폐를 얻을 수 있습니다." to player
            else if arg-1 is "100원":
                give 1 prismarine crystals of unbreaking 2 named "&e&lㅣ  &a₩ 100  &e&lㅣ" with lore "&7진윤서버의 화폐 가치를 지닌 아이템입니다." and "&7많은 곳에서 이 아이템으로 사고팔고를 하실 수 있습니다." and "&7자신의 아이템, 광물을 팔아 화폐를 얻을 수 있습니다." to player
            else if arg-1 is "1000원":
                give 1 prismarine shard of unbreaking 3 named "&e&lㅣ  &a₩ 1,000  &e&lㅣ" with lore "&7진윤서버의 화폐 가치를 지닌 아이템입니다." and "&7많은 곳에서 이 아이템으로 사고팔고를 하실 수 있습니다." and "&7자신의 아이템, 광물을 팔아 화폐를 얻을 수 있습니다." to player
            else if arg-1 is "10000원":
                give 1 sugar cane of unbreaking 4 named "&e&lㅣ  &6₩ 10,000  &e&lㅣ" with lore "&7진윤서버의 화폐 가치를 지닌 아이템입니다." and "&7많은 곳에서 이 아이템으로 사고팔고를 하실 수 있습니다." and "&7자신의 아이템, 광물을 팔아 화폐를 얻을 수 있습니다." to player
            else if arg-1 is "100000원":
                give 1 amethyst shard of unbreaking 5 named "&e&lㅣ  &d₩ 100,000  &e&lㅣ" with lore "&7진윤서버의 화폐 가치를 지닌 아이템입니다." and "&7많은 곳에서 이 아이템으로 사고팔고를 하실 수 있습니다." and "&7자신의 아이템, 광물을 팔아 화폐를 얻을 수 있습니다." to player
            else if arg-1 is "1000000원":
                give 1 nether star of unbreaking 6 named "&e&lㅣ  &d₩ 1,000,000  &e&lㅣ" with lore "&7진윤서버의 화폐 가치를 지닌 아이템입니다." and "&7많은 곳에서 이 아이템으로 사고팔고를 하실 수 있습니다." and "&7자신의 아이템, 광물을 팔아 화폐를 얻을 수 있습니다." to player
            
# Tab complete 기능
on tab complete of "/money":
    if player is op:
        set tab completions for position 1 to "10원", "100원", "1000원", "10000원", "100000원" and "1000000원"


on right click with firework rocket:
    cancel event
    send title "&c[ &f저런! &c]" with subtitle "&6폭죽이 파괴됬습네다!" to player 